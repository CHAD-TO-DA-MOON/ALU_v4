module booleanTester (
    input clk, 
    input rst,
    input start,         // button to begin unit tester, 1: tester wakes up, 0: tester sleeps
    output indicator[2], // indicate if passed test, 1: pass, 0: fail
    output state_indicator[4],
    output counter_indicator[8]
 ) {
	dff counter[4](#INIT(0), .clk(clk), .rst(rst));
	fsm state(.clk(clk), .rst(rst)) = {SLEEP, TEST, PASS, FAIL};
	boolean booleanUnit;

	always{
        indicator = b10;   // 10: pending; 11: pass all the tests; 01: fail the tester
        booleanUnit.x = 16b0;
        booleanUnit.y = 16b0;
        booleanUnit.alufn = 6b0;

        if (start == 0) {
          state.d = state.SLEEP;
          counter.d = 0;
        }
        
        case (state.q){
            state.SLEEP:
                if (start == 1) {state.d = state.TEST;}

            state.TEST:
                case (counter.q){
                    b0001:
                        // AND operation test
                        booleanUnit.alufn = b010000;  
                        booleanUnit.x = 16hF0F0;
                        booleanUnit.y = 16hFF0F;
                        if (booleanUnit.out != 16hF000) {state.d = state.FAIL;} 

                    b0010: 
                        // OR operation test
                        booleanUnit.alufn = b010001;
                        booleanUnit.x = 16hF0F0;
                        booleanUnit.y = 16hFF00;
                        if (booleanUnit.out != 16hFFF0) {state.d = state.FAIL;} 
                    

                    b0011: 
                        // XOR operation test
                        booleanUnit.alufn = b010010;
                        booleanUnit.x = 16hF0F0;
                        booleanUnit.y = 16hFF00;
                        if (booleanUnit.out != 16h0FF0) {state.d = state.FAIL;} 
                    

                    b0100: 
                        // "A" operation test
                        booleanUnit.alufn = b010011;
                        booleanUnit.x = 16hF0F0;
                        booleanUnit.y = 16hFF00;
                        if (booleanUnit.out != 16hF0F0) {state.d = state.FAIL;} 
                    

                    b0101:
                        state.d = state.PASS;
                }

            state.PASS:
                indicator = b11;

            state.FAIL:
                indicator = b01;
        }
        if(state.q == state.TEST) {
          counter.d = counter.q + 1;
        }
        counter_indicator = counter.q;
        case (state.q) {
          state.SLEEP: state_indicator=b0001;
          state.TEST:  state_indicator=b0010;
          state.PASS:  state_indicator=b0100;
          state.FAIL:  state_indicator=b1000;
          default: state_indicator=b1111;
        }
	}
}